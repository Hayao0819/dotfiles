#
# ~/.zshrc
#
#
# Yamada Hayao
# Twitter: @Hayao0819
# Email  : hayao@fascode.net
#
# (c) 2019-2021 Fascode Network.
#


#-- Load config --#
# Source global definitions
for f in "$HOME/.aliases" "$HOME/.env" "$HOME/.env_public"; do
    [[ -f "$f" ]] && source "$f"
done


#-- Completion --#
[ -e /usr/local/share/zsh-completions ] && fpath=(/usr/local/share/zsh-completions $fpath)
if type brew &>/dev/null; then
    FPATH=$(brew --prefix)/share/zsh-completions:$FPATH
fi
autoload -Uz compinit
compinit -u


#-- Load plugin  with sheldon --#
if which sheldon 2> /dev/null 1>&2; then
    eval "$(sheldon source)"
fi


#-- Key --#
# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo
typeset -A key

key[Home]=${terminfo[khome]}
key[End]=${terminfo[kend]}
key[Insert]=${terminfo[kich1]}
key[Delete]=${terminfo[kdch1]}
key[Up]=${terminfo[kcuu1]}
key[Down]=${terminfo[kcud1]}
key[Left]=${terminfo[kcub1]}
key[Right]=${terminfo[kcuf1]}
key[PageUp]=${terminfo[kpp]}
key[PageDown]=${terminfo[knp]}

# setup key accordingly
[[ -n "${key[Home]}"     ]]  && bindkey  "${key[Home]}"     beginning-of-line
[[ -n "${key[End]}"      ]]  && bindkey  "${key[End]}"      end-of-line
[[ -n "${key[Insert]}"   ]]  && bindkey  "${key[Insert]}"   overwrite-mode
[[ -n "${key[Delete]}"   ]]  && bindkey  "${key[Delete]}"   delete-char
[[ -n "${key[Left]}"     ]]  && bindkey  "${key[Left]}"     backward-char
[[ -n "${key[Right]}"    ]]  && bindkey  "${key[Right]}"    forward-char
[[ -n "${key[PageUp]}"   ]]  && bindkey  "${key[PageUp]}"   beginning-of-buffer-or-history
[[ -n "${key[PageDown]}" ]]  && bindkey  "${key[PageDown]}" end-of-buffer-or-history

# Normal history display
if printf "%s\n" $widgets[@] | grep -q "history-substring"; then
    [[ -n "${key[Up]}"       ]]  && bindkey  "${key[Up]}"       history-substring-search-up
    [[ -n "${key[Down]}"     ]]  && bindkey  "${key[Down]}"     history-substring-search-down
else
    [[ -n "${key[Up]}"       ]]  && bindkey  "${key[Up]}"       up-line-or-history
    [[ -n "${key[Down]}"     ]]  && bindkey  "${key[Down]}"     down-line-or-history
fi

# Finally, make sure the terminal is in application mode, when zle is
# active. Only then are the values from $terminfo valid.
if (( ${+terminfo[smkx]} )) && (( ${+terminfo[rmkx]} )); then
    function zle-line-init () {
        printf '%s' "${terminfo[smkx]}"
    }
    function zle-line-finish () {
        printf '%s' "${terminfo[rmkx]}"
    }
    zle -N zle-line-init
    zle -N zle-line-finish
fi


#-- Do not sound --#
setopt nolistbeep
setopt nolistbeep


#-- Save history. --#
HISTFILE=~/.zsh_history
HISTSIZE=100000
SAVEHIST=100000
setopt share_history


#-- Make colors available --#
autoload -Uz colors
colors


#-- Add path --#
if [[ -e "$HOME/.bin" ]]; then
    export PATH="$HOME/.bin:${PATH}"
fi

if [[ -e "$HOME/.local/share/flutter/bin" ]]; then
    export PATH="$HOME/.local/share/flutter/bin:${PATH}"
fi



#-- PROMPT --#
if [[ "$TERM_PROGRAM" = "vscode" ]]; then
    FORCE_POWERLINE_GO=true
fi

if [[ "${TERM}" = "linux" ]]; then
    PROMPT='%B%F{red}%(?..%? )%f%b%B%F{red}%n%f%b@%m %B%40<..<%~%<< %b%# '
elif which starship 2> /dev/null 1>&2 && [[ "${FORCE_POWERLINE_GO-""}" != true ]]; then
    eval "$(starship init zsh)"
elif which powerline-go 2> /dev/null 1>&2; then
    function powerline_precmd() {
        PS1="$(powerline-go -error $? -shell zsh )"
    }

    function install_powerline_precmd() {
        for s in "${precmd_functions[@]}"; do
            if [ "$s" = "powerline_precmd" ]; then
                return
            fi
        done
        precmd_functions+=(powerline_precmd)
    }

    install_powerline_precmd
fi


#-- OS Config --#
case "$(uname -o)" in 
    "Darwin")
        export LIMA_INSTANCE="archlinux"
        export SNDCPY_HOME="$HOME/Applications/Android/sndcpy"
        export SNDCPY_APK="$HOME/Applications/Android/sndcpy/sndcpy.apk"
        export PATH="$SNDCPY_HOME/:$PATH"
        alias sndcpy="sh sndcpy"
        
        export QT_HOMEBREW=true
        export PATH="/Users/hayao/.bin/platform-tools:/opt/homebrew/opt/openjdk@17/bin:$PATH"
        ;;
esac

    if [[ -e "${HOME}/.iterm2_shell_integration.zsh" ]]; then
        source "${HOME}/.iterm2_shell_integration.zsh"
    fi
